scriptencoding utf-8
" ========================================================================
" ä¾äº‘(lilydjwg) çš„ vimrc
" æˆ‘çš„åšå®¢ï¼š http://blog.lilydjwg.me/
"
" æ•´ä¸ªé…ç½®ä»…é€‚ç”¨äºæœ¬äºº
" ä¸è¿‡ï¼Œå…¶ä¸­çš„éƒ¨åˆ†é…ç½®å¾ˆå¯èƒ½éå¸¸é€‚åˆä½ å“¦ï½ï½
" ä¸è¦æ•´ä¸ªåœ°ç…§æ¬ï¼Œåªå¤åˆ¶å¯¹ä½ è‡ªå·±æœ‰ç”¨çš„éƒ¨åˆ†ï¼
"
" æœ‰ä»»ä½•æ„è§å’Œå»ºè®®ï¼Œæˆ–è€…å…¶å®ƒæƒ³è¯´çš„ï¼Œå¯ä»¥åˆ°æˆ‘çš„åšå®¢ç•™è¨€ã€‚
"
" è®¸å¯ï¼šGPLv3
" ========================================================================
" å…¶ä»–æ–‡ä»¶[[[1
try
  packadd matchit
catch /.*/
  runtime macros/matchit.vim
endtry
runtime vimrc_example.vim
"]]]
" æˆ‘çš„è®¾ç½®
" å‡½æ•°[[[1
"   å¤åˆ¶ç¼“å†²åŒºåˆ°æ–°æ ‡ç­¾é¡µ[[[2
function Lilydjwg_copy_to_newtab()
  let temp = tempname()
  try
    let nr = bufnr('%')
    exec "mkview" temp
    tabnew
    exec "buffer" nr
    exec "source" temp
  finally
    call delete(temp)
  endtry
endfunction
"   åˆ é™¤æ‰€æœ‰æœªæ˜¾ç¤ºä¸”æ— ä¿®æ”¹çš„ç¼“å†²åŒºä»¥å‡å°‘å†…å­˜å ç”¨[[[2
function Lilydjwg_cleanbufs()
  for bufNr in filter(range(1, bufnr('$')),
        \ 'buflisted(v:val) && !bufloaded(v:val)')
    execute bufNr . 'bdelete'
  endfor
endfunction
"   è½¬æˆ HTMLï¼Œåªè¦ pre æ ‡ç­¾éƒ¨åˆ†[[[2
"   http://bootleq.blogspot.com/2012/12/tohtml-html-document-function-tohtmldoc.html
function Lilydjwg_to_html(line1, line2)
  let save_number = get(g:, 'html_number_lines', -1)
  let g:html_number_lines = 0
  call tohtml#Convert2HTML(a:line1, a:line2)
  setlocal buftype=nofile bufhidden=hide noswapfile nobuflisted
  call search("<pre[^<]*>")
  normal! dit
  %delete _
  let @" = '<pre>' . substitute(@", '\v^\n\s*', '', '') . '</pre>'
  call setline(1, split(@", '\n'))
  if save_number > -1
    let g:html_number_lines = save_number
  else
    unlet g:html_number_lines
  endif
endfunction
"   è·å–å¯è¯»çš„æ–‡ä»¶å¤§å°[[[2
function Lilydjwg_getfsize(file)
  let size = getfsize(a:file)
  if has('python3')
    try
      py3 from myutils import filesize
      return py3eval('filesize('.size.')')
    catch /.*/
    endtry
  endif
  return size . 'B'
endfunction
"   æ‰“å¼€ NERDTreeï¼Œä½¿ç”¨å½“å‰æ–‡ä»¶ç›®å½•æˆ–è€…å½“å‰ç›®å½•[[[2
function Lilydjwg_NERDTreeOpen()
  if exists("t:NERDTreeBufName")
    NERDTreeToggle
  else
    try
      NERDTree `=expand('%:h')`
    catch /E121/
      NERDTree `=getcwd()`
    endtry
  endif
endfunction
"   Perl-style quoted lists[[[2
function Lilydjwg_qw()
  let in = input('qw(')
  return py3eval('LilyQw("'.escape(in, '"\').'")')
endfunction
"   ä½¿ç”¨åˆ†éš”ç¬¦è¿æ¥å¤šè¡Œ [[[2
function Lilydjwg_join(sep, bang) range
  if a:sep[0] == '\'
    let sep = strpart(a:sep, 1)
  else
    let sep = a:sep
  endif
  let lines = getline(a:firstline, a:lastline)
  if a:firstline == 1 && a:lastline == line('$')
    let dellast = 1
  else
    let dellast = 0
  endif
  exe a:firstline . ',' . a:lastline . 'd_'
  if a:bang != '!'
    call map(lines, "substitute(v:val, '^\\s\\+\\|\\s\\+$', '', 'g')")
  endif
  call append(a:firstline-1, join(lines, sep))
  if dellast
    $d_
  endif
endfunction
"   åˆ‡æ¢æ˜¾ç¤ºè¡Œå·/ç›¸å¯¹è¡Œå·/ä¸æ˜¾ç¤º [[[2
function Lilydjwg_toggle_number()
  if &nu && &rnu
    set nonu nornu
  elseif &nu && !&rnu
    set rnu
  else
    set nu
  endif
endfunction
"   æ›´æ”¹ç¼©è¿›[[[2
function Lilydjwg_reindent(...)
  if a:0 != 2
    echoerr "éœ€è¦ä¸¤ä¸ªå‚æ•°"
  endif
  let save_et = &et
  let save_ts = &ts
  try
    let &ts = a:1
    set noet
    retab!
    let &ts = a:2
    set et
    retab!
    let &l:sw = a:2
  finally
    let &et = save_et
    let &ts = save_ts
  endtry
endfunction
"   å°†å½“å‰çª—å£ç½®äºå±å¹•ä¸­é—´ï¼ˆå…¨å±æ—¶ç”¨ï¼‰[[[2
function CenterFull()
  on
  vs
  ene
  setl nocul
  setl nonu
  40winc |
  winc l
  vs
  winc l
  ene
  setl nocul
  setl nonu
  40winc |
  winc h
  redr!
endfunction
" ä½¿ç”¨ colorpicker ç¨‹åºè·å–é¢œè‰²å€¼(hex/rgba)[[[2
function Lilydjwg_colorpicker()
  if exists("g:last_color")
    let color = substitute(system("colorpicker ".shellescape(g:last_color)), '\n', '', '')
  else
    let color = substitute(system("colorpicker"), '\n', '', '')
  endif
  if v:shell_error == 1
    return ''
  elseif v:shell_error == 2
    " g:last_color å€¼ä¸å¯¹
    unlet g:last_color
    return Lilydjwg_colorpicker()
  else
    let g:last_color = color
    return color
  endif
endfunction
" æ›´æ”¹å…‰æ ‡ä¸‹çš„é¢œè‰²å€¼(hex/rgba/rgb)[[[2
function Lilydjwg_changeColor()
  let color = Lilydjwg_get_pattern_at_cursor('\v\#[[:xdigit:]]{6}(\D|$)@=|<rgba\((\d{1,3},\s*){3}[.0-9]+\)|<rgb\((\d{1,3},\s*){2}\d{1,3}\)')
  if color == ""
    echohl WarningMsg
    echo "No color string found."
    echohl NONE
    return
  endif
  let g:last_color = color
  call Lilydjwg_colorpicker()
  exe 'normal! eF'.color[0]
  call setline('.', substitute(getline('.'), '\%'.col('.').'c\V'.color, g:last_color, ''))
endfunction
" Locate and return character "above" current cursor position[[[2
function LookFurther(down)
  "æ¥æº http://www.ibm.com/developerworks/cn/linux/l-vim-script-1/ï¼Œæœ‰ä¿®æ”¹
  "Locate current column and preceding line from which to copy
  let column_num      = virtcol('.')
  let target_pattern  = '\%' . column_num . 'v.'
  let target_pattern_1  = '\%' . (column_num+1) . 'v.'

  " FIXed å½“å…‰æ ‡ä½äºå¦‚ä¸‹ | æ‰€ç¤ºä½ç½®æ—¶ï¼Œå°†å–å¾—é”™è¯¯çš„è™šæ‹Ÿåˆ—å·
  "          /ä¸­æ–‡
  "          |ä¸­æ–‡
  " å…‰æ ‡ä¸‹çš„å­—ç¬¦æ˜¯å¤šå­—èŠ‚çš„ï¼Ÿ
  " echo '['.matchstr(getline('.'), target_pattern).']'
  if matchstr(getline('.'), target_pattern) == '' &&
	\ matchstr(getline('.'), target_pattern_1) != ''
    let column_num -= 1
    " ä¸Šé¢çš„å­—ç¬¦å¯èƒ½æ˜¯è‹±æ–‡ï¼ˆå‰è€…ï¼‰æˆ–è€…ä¸­æ–‡ï¼ˆåè€…ï¼‰çš„
    let target_pattern  = '\%' . column_num . 'v.\|' . target_pattern
  endif
  if a:down
    let target_line_num = search(target_pattern, 'nW')
  else
    let target_line_num = search(target_pattern, 'bnW')
  endif

  "If target line found, return vertically copied character
  if !target_line_num
    return ""
  else
    return matchstr(getline(target_line_num), target_pattern)
  endif
endfunction
inoremap <silent> <C-Y> <C-R><C-R>=LookFurther(0)<CR>
inoremap <silent> <M-y> <C-R><C-R>=LookFurther(1)<CR>
" å¯¹é½å‘½ä»¤[[[2
function Lilydjwg_Align(type) range
  try
    let pat = g:Myalign_def[a:type]
  catch /^Vim\%((\a\+)\)\=:E716/
    echohl ErrorMsg
    echo "å¯¹é½æ–¹å¼" . a:type . "æ²¡æœ‰å®šä¹‰"
    echohl None
    return
  endtry
  call Align#AlignPush()
  call Align#AlignCtrl(pat[0])
  if len(pat) == 3
    call Align#AlignCtrl(pat[2])
  endif
  exe a:firstline.','.a:lastline."call Align#Align(0, '". pat[1] ."')"
  call Align#AlignPop()
endfunction
function Lilydjwg_Align_complete(ArgLead, CmdLine, CursorPos)
  return filter(keys(g:Myalign_def), 'stridx(v:val, a:ArgLead) == 0')
endfunction
"  é€€æ ¼åˆ é™¤è‡ªåŠ¨ç¼©è¿› [[[2
function! Lilydjwg_checklist_bs(pat)
  " é€€æ ¼å¯æ¸…é™¤è‡ªåŠ¨å‡ºæ¥çš„åˆ—è¡¨ç¬¦å·
  if getline('.') =~ a:pat
    let ind = indent(line('.')-1)
    if !ind
      let ind = indent(line('.')+1)
    endif
    call setline(line('.'), repeat(' ', ind))
    return ""
  else
    return "\<BS>"
  endif
endfunction
"   è¿”å›å½“å‰æ—¥æœŸçš„ä¸­æ–‡è¡¨ç¤º[[[2
function Lilydjwg_zh_date()
  let d = strftime("%Yå¹´%mæœˆ%dæ—¥")
  let d = substitute(d, '[å¹´æœˆ]\@<=0', '', 'g')
  return d
endfunction
"   å…³é—­æŸä¸ªçª—å£[[[2
function Lilydjwg_close(winnr)
  let winnum = bufwinnr(a:winnr)
  if winnum == -1
    return 0
  endif
  " Goto the workspace window, close it and then come back to the
  " original window
  let curbufnr = bufnr('%')
  exe winnum . 'wincmd w'
  close
  " Need to jump back to the original window only if we are not
  " already in that window
  let winnum = bufwinnr(curbufnr)
  if winnr() != winnum
    exe winnum . 'wincmd w'
  endif
  return 1
endfunction
"  è¡¥å…¨ So å‘½ä»¤[[[2
function Lilydjwg_complete_So(ArgLead, CmdLine, CursorPos)
  let path = 'so/' . a:ArgLead . '*'
  let ret = split(globpath(&rtp, path), '\n')
  call filter(ret, 'v:val =~ "\.vim$"')
  " XXX å¦‚æœæ–‡ä»¶åç‰¹æ®Šåˆ™å¯èƒ½ä¸å¯¹
  call map(ret, 'fnamemodify(v:val, '':t:r'')')
  return ret
endfunction
"  å–å¾—å…‰æ ‡å¤„çš„åŒ¹é…[[[2
function Lilydjwg_get_pattern_at_cursor(pat)
  let col = col('.') - 1
  let line = getline('.')
  let ebeg = -1
  let cont = match(line, a:pat, 0)
  while (ebeg >= 0 || (0 <= cont) && (cont <= col))
    let contn = matchend(line, a:pat, cont)
    if (cont <= col) && (col < contn)
      let ebeg = match(line, a:pat, cont)
      let elen = contn - ebeg
      break
    else
      let cont = match(line, a:pat, contn)
    endif
  endwhile
  if ebeg >= 0
    return strpart(line, ebeg, elen)
  else
    return ""
  endif
endfunction
"   åˆ‡æ¢é…è‰²æ–¹æ¡ˆ[[[2
function Lilydjwg_toggle_color()
  let colors = ['pink_lily', 'lilypink', 'darkBlue', 'spring2']
  " spring2 æ˜¯å¢åŠ äº†å½©è‰²ç»ˆç«¯æ”¯æŒçš„ spring
  if !exists("g:colors_name")
    let g:colors_name = 'pink_lily'
  endif
  let i = index(colors, g:colors_name)
  let i = (i+1) % len(colors)
  exe 'colorscheme ' . get(colors, i)
endfunction
"   %xx -> å¯¹åº”çš„å­—ç¬¦(åˆ°æ¶ˆæ¯)[[[2
function Lilydjwg_hexchar()
  let chars = Lilydjwg_get_pattern_at_cursor('\(%[[:xdigit:]]\{2}\)\+')
  if chars == ''
    echohl WarningMsg
    echo 'åœ¨å…‰æ ‡å¤„æœªå‘ç°%è¡¨ç¤ºçš„åå…­è¿›åˆ¶å­—ç¬¦ä¸²ï¼'
    echohl None
    return
  endif
  let str = substitute(chars, '%', '\\x', 'g')
  exe 'echo "'. str . '"'
endfunction
"  ç”¨ç«ç‹æ‰“å¼€é“¾æ¥[[[2
function Lilydjwg_open_url()
  let s:url = Lilydjwg_get_pattern_at_cursor('\v%(https?|ftp)://[^]''" \t\r\n>*ã€‚ï¼Œ\`)]*')
  if s:url == ""
    echohl WarningMsg
    echomsg 'åœ¨å…‰æ ‡å¤„æœªå‘ç°URLï¼'
    echohl None
  else
    echo 'æ‰“å¼€URLï¼š' . s:url
    if has("win32") || has("win64")
      " start ä¸æ˜¯ç¨‹åºï¼Œæ‰€ä»¥æ— æ•ˆã€‚å¹¶ä¸”ï¼Œcmd åªèƒ½ä½¿ç”¨åŒå¼•å·
      " call system("start '" . s:url . "'")
      call system("cmd /q /c start \"" . s:url . "\"")
    elseif has("mac")
      call system("open '" . s:url . "'")
    else
      " call system("gnome-open " . s:url)
      call system("setsid firefox '" . s:url . "' &")
    endif
  endif
  unlet s:url
endfunction
"  Title Save [[[2
function Lilydjwg_TSave()
  let line = getline(1)
  if line =~ '^\s*$'
    let line = getline(2)
  endif
  let line = substitute(line, '[:/\\]', '-', 'g')
  let line = substitute(line, '^\s\+', '', 'g')
  let line = substitute(line, '\s\+$', '', 'g')
  let line = substitute(line, ' ', '\\ ', 'g')
  let line = substitute(line, '\r', '', 'g')
  exe 'sav ' . line . '.txt'
endfunction
"  åˆ‡æ¢ ve [[[2
function Lilydjwg_toggle_ve()
  if &ve == 'all'
    let &ve = ''
  else
    let &ve = 'all'
  endif
endfunction
"  åˆ‡æ¢ ambiwidth [[[2
function Lilydjwg_toggle_ambiwidth()
  if &ambiwidth == 'double'
    let &ambiwidth = 'single'
  else
    let &ambiwidth = 'double'
  endif
endfunction
"  æ˜¯å¦è¯¥è°ƒç”¨ cycleï¼Ÿ[[[2
function Lilydjwg_trycycle(dir)
  let pat = Lilydjwg_get_pattern_at_cursor('[+-]\?\d\+')
  if pat
    if a:dir ==? 'x'
      return "\<C-X>"
    else
      return "\<C-A>"
    end
  else
    let mode = mode() =~ 'n' ? 'w' : 'v'
    let dir = a:dir ==? 'x' ? -1 : 1
    return ":\<C-U>call Cycle('" . mode . "', " . dir . ", v:count1)\<CR>"
  end
endfunction
" set ç›¸å…³[[[1
"   ä¸€èˆ¬è®¾ç½®[[[2
" maybe necessary when root
syntax on
" set guifont=æ–‡æ³‰é©¿ç­‰å®½æ­£é»‘\ Medium\ 10
set number
set smarttab
set expandtab
" ä¸è¦å“é“ƒï¼Œæ›´ä¸è¦é—ªå±
set visualbell t_vb=
" when will this cause problems?
set ttyfast
" ä¸è¦åŒ…å«æ ‡å‡†é”™è¯¯ï¼Œä½†æ˜¯å…è®¸ Vim åˆå§‹åŒ–å…¶é»˜è®¤å€¼
autocmd VimEnter * set shellredir=>
autocmd GUIEnter * set t_vb=
" ! is for histwin to save tags
set viminfo='100,:10000,<50,s10,h,!
set history=10000
set wildmenu
set delcombine " ç»„åˆå­—ç¬¦ä¸€ä¸ªä¸ªåœ°åˆ é™¤
set laststatus=2 " æ€»æ˜¯æ˜¾ç¤ºçŠ¶æ€æ 
" é¦–å…ˆå°è¯•æœ€é•¿çš„ï¼Œæ¥ç€è½®æ¢è¡¥å…¨é¡¹
set wildmode=longest:full,full
set ambiwidth=double
set shiftround
set diffopt+=vertical,context:3,foldcolumn:0
set fileencodings=ucs-bom,utf-8,gb18030,cp936,latin1
set fileformats=unix,dos,mac
set formatoptions=croqn2mB1
try
  " Vim 7.4
  set formatoptions+=j
catch /.*/
endtry
set nojoinspaces
set virtualedit=block
set nostartofline
" set guioptions=egmrLtai
set guioptions=acit
" æ²¡å¿…è¦ï¼Œè€Œä¸”å¾ˆå¤šæ—¶å€™ = è¡¨ç¤ºèµ‹å€¼
set isfname-==
set nolinebreak
set nowrapscan
set scrolloff=5
set sessionoptions=blank,buffers,curdir,folds,help,options,tabpages,winsize,slash,unix,resize
set shiftwidth=2
set winaltkeys=no
set noequalalways
set listchars=eol:$,tab:>-,nbsp:~
set display=lastline
set completeopt+=longest
set maxcombine=4
set cedit=<C-Y>
set whichwrap=b,s,[,]
set tags+=./../tags,./../../tags,./../../../tags
try
  set matchpairs=(:),{:},[:],ã€Š:ã€‹,ã€ˆ:ã€‰,ï¼»:ï¼½,ï¼ˆ:ï¼‰,ã€Œ:ã€,ã€:ã€,â€˜:â€™,â€œ:â€
catch /^Vim\%((\a\+)\)\=:E474/
endtry
" Avoid command-line redraw on every entered character by turning off Arabic
" shaping (which is implemented poorly).
if has('arabic')
  set noarabicshape
endif
set wildignore+=*~,*.py[co],__pycache__,.*.swp
" Linux ä¸ Windows ç­‰ [[[2
if has("gui_macvim")
  set macmeta
end
if has("win32") || has("win64")
  " Win ç¼–ç  [[[3
  " è¡Œç¦åˆ™è¡¥ä¸è¦æ±‚ encoding ä¸º 'utf-8'
  " ä½†æ˜¯è®¾ç½® encoding=utf-8 ä¼šå¯¼è‡´çŠ¶æ€æ å’Œç¼–è¯‘è€…ä¿¡æ¯ä¹±ç 
  " set encoding=utf-8
  " set fileencoding=cp936
  " language messages zh_CN.UTF-8
  " set termencoding=cp936
  " set langmenu=chinese_gb.936
  " source $VIMRUNTIME/delmenu.vim
  " source $VIMRUNTIME/menu.vim
  " Win è·¯å¾„ [[[3
  let g:vimfiles = split(&runtimepath, ',')[1]
  let g:mytmpdir = $TMP
  " Win ç¨‹åº [[[3
  "   ç”¨é»˜è®¤çš„ç¨‹åºæ‰“å¼€æ–‡ä»¶
  nmap <C-S-F5> :!"%"<CR>
  command Hex silent !winhex '%'
  command SHELL silent cd %:p:h|silent exe "!start cmd"|silent cd -
  command Nautilus silent !explorer %:p:h
  " Win é…ç½® [[[3
  command FScreen simalt ~x
  command Fscreen simalt ~r
  if has('directx')
    set renderoptions=type:directx
  endif
else
  " Linux è·¯å¾„ [[[3
  let g:vimfiles = split(&runtimepath, ',')[0]
  if exists('$VIMTMP')
    let g:mytmpdir = $VIMTMP
  else
    let g:mytmpdir = expand("~/tmpfs")
  endif
  let g:MuttVim_configfile = expand('~/scripts/python/pydata/muttvim.json')
  cnoremap <expr> <C-T> getcmdtype() == ':' ? '~/tmpfs/' : "\<C-t>"
  " cron çš„ç›®å½•ä¸è¦å¤‡ä»½
  set backupskip+=/etc/cron.*/*
  set backupskip+=*/HTTPSEverywhereUserRules/*
  set backupdir=.,/var/tmp,/tmp
  " Linux ç¨‹åº [[[3
  "   ç”¨é»˜è®¤çš„ç¨‹åºæ‰“å¼€æ–‡ä»¶
  nmap <C-S-F5> :!gnome-open "%"<CR>
  set grepprg=grep\ -nH\ $*
  command Hex silent !setsid ghex2 '%'
  command SHELL silent cd %:p:h|silent exe '!setsid xfce4-terminal'|silent cd -
  command Nautilus silent !nautilus %:p:h
  " Linux é…ç½® [[[3
  command FScreen winpos 0 0|set lines=40|set columns=172
  command Fscreen set lines=40|set columns=88
endif
" è¯­è¨€ç›¸å…³ [[[3
if $LANGUAGE =~ '^zh' || ($LANGUAGE == '' && v:lang =~ '^zh')
  " ç¼“å†²åŒºå· æ–‡ä»¶å è¡Œæ•° ä¿®æ”¹ å¸®åŠ© åªè¯» ç¼–ç  æ¢è¡Œç¬¦ BOM ======== å­—ç¬¦ç¼–ç  ä½ç½® ç™¾åˆ†æ¯”ä½ç½®
  set statusline=%n\ %<%f\ %Lè¡Œ\ %{&modified?'[+]':&modifiable\|\|&ft=~'^\\vhelp\|qf$'?'':'[-]'}%h%r%{&fenc=='utf-8'\|\|&fenc==''?'':'['.&fenc.']'}%{&ff=='unix'?'':'['.&ff.']'}%{&bomb?'[BOM]':''}%{&eol?'':'[noeol]'}%{&diff?'[diff]':''}%=\ 0x%-4.8B\ \ \ \ %-14.(%l,%c%V%)\ %P
else
  set statusline=%n\ %<%f\ %LL\ %{&modified?'[+]':&modifiable\|\|&ft=~'^\\vhelp\|qf$'?'':'[-]'}%h%r%{&fenc=='utf-8'\|\|&fenc==''?'':'['.&fenc.']'}%{&ff=='unix'?'':'['.&ff.']'}%{&bomb?'[BOM]':''}%{&eol?'':'[noeol]'}%{&diff?'[diff]':''}%=\ 0x%-4.8B\ \ \ \ %-14.(%l,%c%V%)\ %P
endif
" è·¯å¾„ç›¸å…³ [[[3
let g:VEConf_favorite = g:vimfiles . "/ve_favorite"
let g:NERDTreeBookmarksFile = g:vimfiles . "/NERDTreeBookmarks"
if has("python3")
  exe "py3file" g:vimfiles . "/vimrc.py"
endif
let g:undodir = g:mytmpdir . "/.vimundo"
let &errorfile= g:mytmpdir . "/.error"
" å›¾å½¢ä¸ç»ˆç«¯ [[[2
let colorscheme = 'lilypink'
if has("gui_running")
  set mousemodel=popup
  " æœ‰äº›ç»ˆç«¯ä¸èƒ½æ”¹å˜å¤§å°
  set columns=88
  set lines=38
  set cursorline
  exe 'colorscheme' colorscheme
elseif has("unix")
  set ambiwidth=single
  " é˜²æ­¢é€€å‡ºæ—¶ç»ˆç«¯ä¹±ç 
  " è¿™é‡Œä¸¤è€…éƒ½éœ€è¦ã€‚åªå‰è€…æ ‡é¢˜ä¼šé‡å¤ï¼Œåªåè€…ä¼šä¹±ç 
  set t_fs=(B
  set t_IE=(B
  if &term =~ '256color\|nvim'
    set cursorline
    exe 'colorscheme' colorscheme
  else
    " åœ¨Linuxæ–‡æœ¬ç»ˆç«¯ä¸‹éæ’å…¥æ¨¡å¼æ˜¾ç¤ºå—çŠ¶å…‰æ ‡
    if &term == "linux" || &term == "fbterm"
      set t_ve+=[?6c
      autocmd InsertEnter * set t_ve-=[?6c
      autocmd InsertLeave * set t_ve+=[?6c
      " autocmd VimLeave * set t_ve-=[?6c
    endif
    if &term == "fbterm"
      set cursorline
      exe 'colorscheme' colorscheme
    elseif $TERMCAP =~ 'Co#256'
      set t_Co=256
      set cursorline
      exe 'colorscheme' colorscheme
    else
      " æš‚æ—¶åªæœ‰è¿™ä¸ªé…è‰²æ¯”è¾ƒé€‚åˆäº†
      colorscheme default
      " åœ¨ç»ˆç«¯ä¸‹ï¼Œå¦‚æœç è¡¨å­˜åœ¨ï¼Œåˆ™è‡ªåŠ¨åŠ è½½vimimè¾“å…¥æ³•
      if len(split(globpath(&rtp, 'so/vimim.wubi.txt'), '\n')) > 0
	autocmd VimEnter * runtime so/vimim.vim
      endif
    endif
  endif
elseif has('win32') && exists('$CONEMUBUILD')
  " enable 256 colors in ConEmu on Win
  set term=xterm
  set t_Co=256
  let &t_AB="\e[48;5;%dm"
  let &t_AF="\e[38;5;%dm"
  set cursorline
  exe 'colorscheme' colorscheme
endif
unlet colorscheme
" bracketed paste mode support for tmux
if &term =~ '^screen\|^tmux' && exists('&t_BE')
  let &t_BE = "\033[?2004h"
  let &t_BD = "\033[?2004l"
  " t_PS and t_PE are key code options and they are special
  exec "set t_PS=" . "\033[200~"
  exec "set t_PE=" . "\033[201~"
endif
" ä¸åŒçš„ Vim ç‰ˆæœ¬ [[[2
if has("conceal")
  " 'i' is for neosnippet
  set concealcursor=nci
  set conceallevel=2
endif
if has("persistent_undo")
  let &undodir=g:undodir
  if !isdirectory(&undodir)
    call mkdir(&undodir, 'p', 0700)
  endif
  set undofile
endif
try
  " Vim 7.4.399+
  set cryptmethod=blowfish2
catch /.*/
  " Vim 7.3+
  try
    set cryptmethod=blowfish
  catch /.*/
    " Vim 7.2-, neovim
  endtry
endtry
unlet g:undodir
let g:silent_unsupported = 1
" map ç›¸å…³[[[1
"   nmap [[[2
"     Fx ç›¸å…³ [[[3
" buffer list
nmap <F2> <Leader>be
nmap <F4> :ls<CR>:buffer 
nmap <F6> :cnext<CR>
nmap <S-F6> :cprevious<CR>
nmap <silent> <F9> :enew<CR>
nmap <silent> <F8> :GundoToggle<CR>
nmap <F11> :next<CR>
nmap <S-F11> :previous<CR>
" é‡æ–°è½½å…¥å½“å‰æ–‡ä»¶
nmap <F5> :e!<CR>
"     t å¼€å¤´ [[[3
nmap <silent> tt :tabnew<CR>
nmap <silent> TT :call Lilydjwg_copy_to_newtab()<CR>
" format all
nmap t= mxHmygg=G`yzt`x
" select all
nmap ta ggVG
nmap <silent> tf :call Lilydjwg_open_url()<CR>
" less style æ¸…é™¤é«˜äº®
nmap <silent> <M-u> :nohls<CR>
" join line without space
nmap tj Jx
" select line content
nnoremap tl ^vg_
nmap <silent> to :call append('.', '')<CR>j
nmap <silent> tO :call append(line('.')-1, '')<CR>k
nmap tp "+P
nmap <silent> tv :call Lilydjwg_toggle_ve()<CR>
nmap tw :call Lilydjwg_toggle_ambiwidth()<CR>
"     w å¼€å¤´ [[[3
nmap wc :set cursorline!<CR>
nnoremap <silent> wf :call Lilydjwg_NERDTreeOpen()<CR>
nnoremap <silent> wn :call Lilydjwg_toggle_number()<CR>
nnoremap <silent> wt :TlistToggle<CR>
nnoremap <silent> wb :TagbarToggle<CR>
"     - å¼€å¤´ [[[3
nmap -+ :set nomodified<CR>
nmap -c :call Lilydjwg_toggle_color()<CR>
nmap -ft :exe 'tabe '.g:vimfiles.'/ftplugin/'.&ft.'.vim'<CR>
nmap -syn :exe 'tabe '.g:vimfiles.'/syntax/'.&ft.'.vim'<CR>
nmap -int :exe 'tabe '.g:vimfiles.'/indent/'.&ft.'.vim'<CR>
"     æ˜¾ç¤ºé«˜äº®ç»„ [[[4
nnoremap <silent> wh :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans<' . synIDattr(synID(line("."),col("."),0),"name") . "> lo<" . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<CR>
"     Alt ç»„åˆé”® [[[3
nmap <M-m> :MRU 
" æ‰“å¼€è‰ç¨¿
nmap <unique> <silent> <M-s> <Plug>ShowScratchBuffer
for i in range(1, 8)
  exec 'nnoremap <silent> <M-' . i . '> '. i .'gt'
endfor
nnoremap <silent> <M-9> :exec "normal!" min([tabpagenr('$'),9])."gt"<CR>
"     lusty-explorer [[[4
nmap <M-b> :LustyBufferExplorer<CR>
nmap <M-g> :LustyBufferGrep<CR>
nmap <M-l> :LustyFilesystemExplorerFromHere<CR>
let g:LustyExplorerSuppressRubyWarning = 1
"     å…¶å®ƒå¼€å¤´çš„ [[[3
nmap <silent> <C-Tab> :tabnew<CR>
nmap <BS> <C-O>
nmap <C-D> <C-W>q
nnoremap <Space> za
nmap ' <C-W>
nmap Y y$
nmap 'm :MarksBrowser<CR>
nmap :: :!
nmap cd :lcd %:p:h<CR>:echo expand('%:p:h')<CR>
nmap gb :setl fenc=gb18030<CR>
nmap d<CR> :%s/\r//eg<CR>``
nmap cac :call Lilydjwg_changeColor()<CR>
nmap gl :IndentGuidesToggle<CR>
nnoremap <silent> gs :echo Lilydjwg_getfsize(expand('%'))<CR>
"   imap [[[2
inoremap <S-CR> <CR>    
inoremap <M-c> <C-R>=Lilydjwg_colorpicker()<CR>
inoremap <C-J> <C-P>
inoremap <M-j> <C-N>
inoremap <M-q> <C-R>=Lilydjwg_qw()<CR>
imap <S-BS> <C-W>
cmap <S-BS> <C-W>
"     æ—¥æœŸå’Œæ—¶é—´ [[[3
imap <silent> <F5> <C-R>=Lilydjwg_zh_date()<CR>
imap <silent> <S-F5> <C-R>=strftime("%Y-%m-%d")<CR>
imap <silent> <C-F5> <C-R>=strftime("%Y-%m-%d %H:%M")<CR>
"     è¡¥å…¨ [[[3
imap <F2> <C-X><C-O>
imap <F3> <C-X><C-F>
imap <S-F3> <C-X><C-L>
"     è¡¥å…¨æœ€é•¿é¡¹
inoremap <expr> <C-L> pumvisible()?"\<C-E>\<C-N>":"\<C-N>"
"   vmap [[[2
vnoremap <Leader># "9y?<C-R>='\V'.substitute(escape(@9,'\?'),'\n','\\n','g')<CR><CR>
vnoremap <Leader>* "9y/<C-R>='\V'.substitute(escape(@9,'\/'),'\n','\\n','g')<CR><CR>
vnoremap <C-C> "+y
"     ä¸­æ–‡å¼•å· [[[3
vmap â€œ <ESC>`<iâ€œ<ESC>`>aâ€<ESC>
vmap â€ <ESC>`>aâ€<ESC>`<iâ€œ<ESC>
"   cmap [[[2
"     è¿˜æ˜¯è¿™æ ·å§
"     FIXME ä½†è¿™æ ·åœ¨ wildmenu è¡¥å…¨æ—¶ä¼šæœ‰ç‚¹å¥‡æ€ª
cmap <C-P> <Up>
cmap <C-N> <Down>
cnoremap <Left> <Space><BS><Left>
cnoremap <Right> <Space><BS><Right>
"   g[jk] [[[2
nmap <M-j> gj
nmap <M-k> gk
vmap <M-j> gj
vmap <M-k> gk
"     ä»¥ % è¡¨ç¤ºçš„å­—ç¬¦ [[[2
vmap <silent> t% :w !ascii2uni -a J -q<CR>
nmap <silent> t% :call Lilydjwg_hexchar()<CR>
"     HTML è½¬ä¹‰ [[[2
"     I got the idea from unimpaired.vim
noremap <silent> [x :HTMLEscape<CR>
noremap <silent> ]x :HTMLUnescape<CR>
nnoremap <silent> [x :.HTMLEscape<CR>
nnoremap <silent> ]x :.HTMLUnescape<CR>
"     Ctrl-S ä¿å­˜æ–‡ä»¶ [[[2
nmap <silent> <C-S> :update<CR>
imap <silent> <C-S> <ESC>:update<CR>
vmap <silent> <C-S> <ESC>:update<CR>
"     å¿«é€Ÿéšè—å½“å‰çª—å£å†…å®¹[[[2
nmap <F12> :tabnew<CR>
imap <F12> <ESC>:tabnew<CR>
vmap <F12> <ESC>:tabnew<CR>
"     Shift+é¼ æ ‡æ»šåŠ¨[[[2
if v:version < 703
  nmap <silent> <S-MouseDown> zhzhzh
  nmap <silent> <S-MouseUp> zlzlzl
  vmap <silent> <S-MouseDown> zhzhzh
  vmap <silent> <S-MouseUp> zlzlzl
else
  map <S-ScrollWheelDown> <ScrollWheelRight>
  map <S-ScrollWheelUp> <ScrollWheelLeft>
  imap <S-ScrollWheelDown> <ScrollWheelRight>
  imap <S-ScrollWheelUp> <ScrollWheelLeft>
endif
"     Shift+é¼ æ ‡ä¸­é”®[[[2
nnoremap <silent> <S-MiddleMouse> "+P
inoremap <silent> <S-MiddleMouse> <C-R>+
"     ä¸Šä¸‹ç§»åŠ¨ä¸€è¡Œæ–‡å­—[[[2
nmap <C-j> mz:m+<cr>`z
nmap <C-k> mz:m-2<cr>`z
vmap <C-j> :m'>+<cr>`<my`>mzgv`yo`z
vmap <C-k> :m'<-2<cr>`>my`<mzgv`yo`z
" è‡ªåŠ¨å‘½ä»¤[[[1
"   è‡ªåŠ¨å…³é—­é¢„è§ˆçª—å£ï¼ˆä¸èƒ½ç”¨åœ¨å‘½ä»¤çª—å£ï¼Œæ‰€ä»¥è®¾ç½®äº†ä¸€ä¸ªå˜é‡ï¼‰
let s:cmdwin = 0
autocmd CmdwinEnter	* let s:cmdwin = 1
autocmd CmdwinLeave	* let s:cmdwin = 0
autocmd InsertLeave	* if s:cmdwin == 0 && pumvisible() == 0|pclose|endif
"   æ’å…¥æ¨¡å¼ä¸‹é•¿æ—¶é—´ä¸åŠ¨åˆ™æ‰“æ–­æ’’æ¶ˆåºåˆ—
if (v:version == 800 && has("patch1407")) || v:version != 800
  autocmd CursorHoldI * call feedkeys("\<C-g>u", 'nt')
endif
autocmd BufReadCmd *.maff,*.xmind,*.crx,*.apk,*.whl,*.egg  call zip#Browse(expand("<amatch>"))
"   è§ ft-syntax-omni
if has("autocmd") && exists("+omnifunc")
  autocmd Filetype *
	\ if &omnifunc == "" |
	\   setlocal omnifunc=syntaxcomplete#Complete |
	\ endif
endif
" è‡ªå®šä¹‰å‘½ä»¤[[[1
" å¯¹é½ xxx: xxx ï¼ˆä¸¤æ ï¼‰
" .vimrc æœ‰å¯èƒ½æ˜¯è½¯é“¾æ¥
exe 'command Set tabe ' . escape(resolve($MYVIMRC), ' ')
" åˆ é™¤å½“å‰æ–‡ä»¶
command Delete if delete(expand('%')) | echohl WarningMsg | echo "åˆ é™¤å½“å‰æ–‡ä»¶å¤±è´¥" | echohl None | endif
command -nargs=1 -range=% -bang Join <line1>,<line2>call Lilydjwg_join(<q-args>, "<bang>")
command -nargs=+ Reindent call Lilydjwg_reindent(<f-args>)
" TODO better implement
command -range=% ClsXML <line1>,<line2>!tidy -utf8 -iq -xml
command -range=% ClsHTML <line1>,<line2>!tidy -utf8 -iq -omit -w 0
command -range=% ClsJSON setf json | <line1>,<line2>!jq .
command MB tabe ~/temp/mb
command -nargs=1 -complete=customlist,Lilydjwg_complete_So So runtime so/<args>.vim
command -nargs=1 Delmark delm <args>|wviminfo!
"   åˆ é™¤ç©ºè¡Œ
command -range=% -bar DBlank <line1>,<line2>g/^\s*$/d_|nohls
"   æŸä¸ª pattern å‡ºç°çš„æ¬¡æ•°
command -range=% -nargs=1 Count <line1>,<line2>s/<args>//gn|nohls
command -range=% -bar SBlank <line1>,<line2>s/\v(^\s*$\n){2,}/\r/g
"   åˆ é™¤æ‹–å°¾çš„ç©ºç™½
command -range=% -bar TWS <line1>,<line2>s/\s\+$//|nohls|normal ``
"   è®¾ç½®æˆ Linux ä¸‹é€‚ç”¨çš„æ ¼å¼
command Lin setl ff=unix fenc=utf8 nobomb eol
"   è®¾ç½®æˆ Windows ä¸‹é€‚ç”¨çš„æ ¼å¼
command Win setl ff=dos fenc=gb18030
"   ä»¥ç¬¬ä¸€è¡Œçš„æ–‡å­—ä¸ºåä¿å­˜å½“å‰æ–‡ä»¶
command TSave call Lilydjwg_TSave()
command -nargs=? -complete=file RSplit vs <args>|normal <C-W>L<C-W>w
command -range=% -bar SQuote <line1>,<line2>s/â€œ\|â€\|â€³/"/ge|<line1>,<line2>s/â€˜\|â€™\|â€²/'/ge
command -range -bar HTMLEscape <line1>,<line2>s/&/\&amp;/ge|<line1>,<line2>s/</\&lt;/ge|<line1>,<line2>s/>/\&gt;/ge
command -range -bar HTMLUnescape <line1>,<line2>s/&amp;/\&/ge|<line1>,<line2>s/&lt;/</ge|<line1>,<line2>s/&gt;/>/ge
"   ç”¨ VimExplorer æ’ä»¶æ‰“å¼€å½“å‰æ–‡ä»¶æ‰€åœ¨çš„ç›®å½•
command Path VE %:p:h
command -nargs=1 Enc e ++bad=keep ++enc=<args> %
command CenterFull call CenterFull()
command -nargs=1 -range=% -complete=customlist,Lilydjwg_Align_complete LA <line1>,<line2>call Lilydjwg_Align("<args>")
command -nargs=1 -range=% Column <line1>,<line2>Align! w<args>0P1p \S\@<=\s\@=
command -range=% Paste <line1>,<line2>py3 LilyPaste()
command -range=% Tohtml call Lilydjwg_to_html(<line1>, <line2>)
command BufClean call Lilydjwg_cleanbufs()
command Helptags for d in filter(globpath(&rtp, "doc/", 0, 1), 'filewritable(v:val)') | exec "helptags" d | endfor
" æ’ä»¶é…ç½®[[[1
"   ft-rust[[[2
let g:rust_fold = 1
"   asyncrun.vim[[[2
command! -bang -nargs=* -complete=file Make AsyncRun -program=make @ <args>
"   choosewin[[[2
nmap <M-w> <Plug>(choosewin)
let g:choosewin_overlay_enable = 1
let g:choosewin_statusline_replace = 0
"   mark.vim[[[2
let g:mwDefaultHighlightingPalette = 'maximum'
"   extradite.vim[[[2
let g:extradite_showhash = 1
"   linediff[[[2
let g:linediff_buffer_type = 'scratch'
"   rst_tables[[[2
let g:rst_tables_no_warning = 1
"   signify [[[2
let g:signify_vcs_list = ['git']
let g:signify_sign_overwrite = 0
" signify won't update on FocusGained anymore
let g:signify_disable_by_default = 1
"   ConflictMotions [[[2
" ç¦ç”¨ \x å¼€å¤´çš„æ˜ å°„ï¼›å®ƒä»¬ä¸ EnhancedCommentify å†²çªäº†
let g:ConflictMotions_TakeMappingPrefix = ''
"   surround [[[2
"      æ¯”èµ· cï¼Œæˆ‘æ›´å–œæ¬¢ç”¨ s
let g:surround_no_mappings = 1
" original
nmap ds  <Plug>Dsurround
nmap ys  <Plug>Ysurround
nmap yS  <Plug>YSurround
nmap yss <Plug>Yssurround
nmap ySs <Plug>YSsurround
nmap ySS <Plug>YSsurround
xmap S   <Plug>VSurround
xmap gS  <Plug>VgSurround
imap <C-G>s <Plug>Isurround
imap <C-G>S <Plug>ISurround
" mine
xmap c <Plug>VSurround
xmap C <Plug>VSurround
" cs is for cscope
nmap cS <Plug>Csurround
"   NrrRgn[[[2
let g:nrrw_rgn_vert = 1
let g:nrrw_rgn_wdth = 80
let g:nrrw_rgn_hl = 'Folded'
"   easymotion[[[2
let EasyMotion_leader_key = '<M-q>'
let EasyMotion_keys = 'abcdefghijklmnopqrstuvwxyz'
"   cycle[[[2
"   https://github.com/lilydjwg/vim-cycle
nnoremap <expr> <silent> <C-X> Lilydjwg_trycycle('x')
vnoremap <expr> <silent> <C-X> Lilydjwg_trycycle('x')
nnoremap <expr> <silent> <C-A> Lilydjwg_trycycle('p')
vnoremap <expr> <silent> <C-A> Lilydjwg_trycycle('p')
nnoremap <Plug>CycleFallbackNext <C-A>
nnoremap <Plug>CycleFallbackPrev <C-X>
let g:cycle_no_mappings = 1
let g:cycle_default_groups = [
      \ [['true', 'false']],
      \ [['yes', 'no']],
      \ [['and', 'or']],
      \ [['on', 'off']],
      \ [['>', '<']],
      \ [['==', '!=']],
      \ [['æ˜¯', 'å¦']],
      \ [['æœ‰', 'æ— ']],
      \ [["in", "out"]],
      \ [["min", "max"]],
      \ [["get", "post"]],
      \ [["to", "from"]],
      \ [["read", "write"]],
      \ [['with', 'without']],
      \ [["exclude", "include"]],
      \ [["asc", "desc"]],
      \ [["next", "prev"]],
      \ [["encode", "decode"]],
      \ [["left", "right"]],
      \ [["hide", "show"]],
      \ [['ã€Œ:ã€', 'ã€:ã€'], 'sub_pairs'],
      \ [['Sunday', 'Monday', 'Tuesday', 'Wednesday', 'Thursday',
      \ 'Friday', 'Saturday'], 'hard_case', {'name': 'Days'}],
      \ [["enable", "disable"]],
      \ [["add", "remove"]],
      \ [['up', 'down']],
      \ [['after', 'before']],
      \ ]
"   Erlang[[[2
let g:erlangHighlightBif = 1
let g:erlangFold = 1
"   CountJump[[[2
"   Regex in Javascript, etc
call CountJump#TextObject#MakeWithCountSearch('', '/', 'ai', 'v', '\\\@<!/', '\\\@<!/')
call CountJump#TextObject#MakeWithCountSearch('', ':', 'ai', 'v', '\\\@<!:', '\\\@<!:')
call CountJump#TextObject#MakeWithCountSearch('', '_', 'ai', 'v', '_', '_')
call CountJump#TextObject#MakeWithCountSearch('', '<Tab>', 'ai', 'v', '\t', '\t')
"   colorizer.vim[[[2
let g:colorizer_nomap = 1
let g:colorizer_startup = 0
"   grep.vim[[[2
let g:Grep_Default_Options = '--binary-files=without-match'
"   NERDTree[[[2
let g:NERDTreeMapToggleZoom = 'a'
let g:NERDTreeMapToggleHidden = 'h'
"   å¦è§å¹³å°ç›¸å…³éƒ¨åˆ†
"   DirDiff[[[2
let g:DirDiffDynamicDiffText = 1
let g:DirDiffExcludes = "*~,*.swp"
let g:DirDiffWindowSize = 20
"   gundo[[[2
let gundo_preview_bottom = 1
let gundo_prefer_python3 = 1
"   bufexplorer[[[2
let g:bufExplorerFindActive = 0
"   tagbar[[[2
let g:tagbar_type_dosini = {
      \ 'ctagstype': 'ini',
      \ 'kinds': ['s:sections', 'b:blocks'],
      \ }
let g:tagbar_type_pgsql = {
      \ 'ctagstype': 'pgsql',
      \ 'kinds': ['f:functions', 't:tables'],
      \ }
"   taglist[[[2
let Tlist_Show_One_File = 1
let tlist_vimwiki_settings = 'wiki;h:headers'
let tlist_tex_settings = 'latex;h:headers'
let tlist_tracwiki_settings = 'wiki;h:headers'
let tlist_diff_settings = 'diff;m:modified;n:created;d:deleted;h:hunks'
let tlist_git_settings = 'diff;m:modified;n:created;d:deleted;h:hunks'
let tlist_gitcommit_settings = 'gitcommit;f:file'
let tlist_privoxy_settings = 'privoxy;s:sections'
"  æ¥æº http://gist.github.com/476387
let tlist_html_settings = 'html;h:Headers;o:IDs;c:Classes'
let tlist_dosini_settings = 'ini;s:sections'
let tlist_pgsql_settings = 'pgsql;f:functions;t:tables'
let tlist_markdown_settings = 'markdown;h:headers'
let tlist_rust_settings = 'rust;n:modules;s:structural types;i:trait interfaces;c:implementations;f:functions;g:enums;t:type aliases;v:global variables;M:macro definitions;m:struct fields;e:enum variants;F:methods'
hi link MyTagListFileName Type
"   2html.vim, ä½¿ç”¨XHTMLæ ¼å¼[[[2
let use_xhtml = 1
"   shell è„šæœ¬æ‰“å¼€æŠ˜å 
let g:sh_fold_enabled = 3 " æ‰“å¼€å‡½æ•°å’Œ here æ–‡æ¡£çš„æŠ˜å 
"   Align[[[2
let g:Align_xstrlen = 4 " use strdisplaywidth
"   Lilydjwg_Align
"   Meanings:
"     colon:     dict definition like 'key: value,'
"     colonl:    list items like this one
"     comment:   #-style comments
"     jscomment: //-style comments
let g:Myalign_def = {
      \   'colon':     ['WP0p1l:', ':\@<=', 'g ,'],
      \   'colonl':    ['WP0p1l:', ':\@<='],
      \   'comma':     ['WP0p1l:', ',\@<=', 'g ,'],
      \   'commalist': ['WP0p1l', ',\@<=', 'g ,'],
      \   'comment':   ['WP1p1l:', '#'],
      \   'css':       ['WP0p1l:', ':\@<=', 'v \v^\s*/\*|\{|\}'],
      \   'define':    ['WP0p1l:', ' \d\@=', 'g ^#define\s'],
      \   'jscomment': ['WP0p1l:', '//'],
      \ }
"   EnhancedCommentify[[[2
let g:EnhCommentifyRespectIndent = 'Yes'
let g:EnhCommentifyUseSyntax = 'Yes'
let g:EnhCommentifyPretty = 'Yes'
let g:EnhCommentifyBindInInsert = 'No'
let g:EnhCommentifyMultiPartBlocks = 'Yes'
let g:EnhCommentifyCommentsOp = 'Yes'
let g:EnhCommentifyAlignRight = 'Yes'
let g:EnhCommentifyUseBlockIndent = 'Yes'
"   indent/html.vim[[[2
let g:html_indent_inctags = "html,body,head,tbody,p,li,dd,marquee,header,nav,article,section"
let g:html_indent_script1 = "inc"
let g:html_indent_style1 = "inc"
"   mru[[[2
let MRU_File = g:vimfiles . '/vim_mru_files'
let MRU_Max_Entries = 2000
let MRU_Exclude_Files = '\v^.*\~$|/COMMIT_EDITMSG$|/itsalltext/|^/tmp/'
"  åŠ è½½èœå•å¤ªè€—æ—¶
let MRU_Add_Menu = 0
let MRU_Filename_Format = {
    \   'formatter': 'v:val',
    \   'parser': '.*',
    \   'syntax': '[^/]\+$'
    \ }
"   syntax/haskell.vim[[[2
let hs_highlight_boolean = 1
let hs_highlight_types = 1
let hs_highlight_more_types = 1
"   syntax/python.vim[[[2
let python_highlight_all = 1
"   syntax/vim.vim é»˜è®¤ä¼šé«˜äº® s:[a-z] è¿™æ ·çš„å‡½æ•°åä¸ºé”™è¯¯[[[2
let g:vimsyn_noerror = 1
let g:netrw_list_hide = '^\.[^.].*'
"   tasklist[[[2
let g:tlTokenList = ["FIXME", "TODO", "XXX", "NotImplemented", "unimplemented!()"]
"   vimExplorer[[[2
let g:VEConf_showHiddenFiles = 0
"   å¦è§å¹³å°ç›¸å…³éƒ¨åˆ†
"   ä¸è¦å ç”¨ ' çš„æ˜ å°„
let g:VEConf_fileHotkey = {}
let g:VEConf_fileHotkey.gotoPlace = '`'
let g:VEConf_fileHotkey.help = '<F1>'
let g:VEConf_treeHotkey = {}
let g:VEConf_treeHotkey.help = '<F1>'
let g:VEConf_treeHotkey.toggleNode = '<Space>'
"   Vimim[[[2
let g:vimim_map = 'c-bslash,c-space'
"   vimwiki[[[2
let g:vimwiki_list = [{'path': '~/.vimwiki/'}]
let g:vimwiki_camel_case = 0
let g:vimwiki_hl_cb_checked = 1
let g:vimwiki_folding = 0
let g:vimwiki_browsers = ['firefox']
let g:vimwiki_CJK_length = 1
let g:vimwiki_dir_link = 'index'
let g:vimwiki_html_header_numbering = 2
let g:vimwiki_conceallevel = 2
"   xml.vimï¼Œä½¿æ‰€æœ‰çš„æ ‡ç­¾éƒ½å…³é—­[[[2
let xml_use_xhtml = 1
"   netrwï¼Œelinksä¸è¡Œï¼Œä½¿ç”¨curlå§
if executable("curl")
  let g:netrw_http_cmd  = "curl"
  let g:netrw_http_xcmd = "-L --compressed -o"
endif
" cscope setting [[[1
if has("cscope")
  " support GNU Global [[[2
  let s:tags_files = []
  if executable("gtags-cscope")
    call add(s:tags_files, ['GTAGS', 'gtags-cscope'])
  endif
  if executable("cscope")
    call add(s:tags_files, ['cscope.out', 'cscope'])
  endif

  if !empty(s:tags_files)
    " settings and autocmd [[[2
    set csto=1
    set cst
    set cscopequickfix=s-,c-,d-,i-,t-,e-

    " add any database in current directory
    function Lilydjwg_csadd()
      try
        cd %:h
      catch /.*/
        return
      endtry

      try
        for [filename, prgname] in s:tags_files
          let db = findfile(filename, '.;')
          if !empty(db)
            let &cscopeprg = prgname
            set nocscopeverbose
            exec "cs add" db expand('%:p:h')
            set cscopeverbose
            break
          endif
        endfor
      finally
        silent cd -
      endtry
    endfunction

    autocmd BufRead *.c,*.cpp,*.h,*.cc,*.java call Lilydjwg_csadd()

    " æ˜ å°„ [[[2
    " æŸ¥æ‰¾Cè¯­è¨€ç¬¦å·ï¼Œå³æŸ¥æ‰¾å‡½æ•°åã€å®ã€æšä¸¾å€¼ç­‰å‡ºç°çš„åœ°æ–¹
    nmap css :cs find s <C-R>=expand("<cword>")<CR><CR>
    " æŸ¥æ‰¾å‡½æ•°ã€å®ã€æšä¸¾ç­‰å®šä¹‰çš„ä½ç½®ï¼Œç±»ä¼¼ctagsæ‰€æä¾›çš„åŠŸèƒ½
    nmap csg :cs find g <C-R>=expand("<cword>")<CR><CR>
    " æŸ¥æ‰¾æœ¬å‡½æ•°è°ƒç”¨çš„å‡½æ•°
    nmap csd :cs find d <C-R>=expand("<cword>")<CR><CR>
    " æŸ¥æ‰¾è°ƒç”¨æœ¬å‡½æ•°çš„å‡½æ•°
    nmap csc :cs find c <C-R>=expand("<cword>")<CR><CR>
    " æŸ¥æ‰¾æŒ‡å®šçš„å­—ç¬¦ä¸²
    nmap cst :cs find t <C-R>=expand("<cword>")<CR><CR>
    " æŸ¥æ‰¾egrepæ¨¡å¼ï¼Œç›¸å½“äºegrepåŠŸèƒ½ï¼Œä½†æŸ¥æ‰¾é€Ÿåº¦å¿«å¤šäº†
    nmap cse :cs find e <C-R>=expand("<cword>")<CR><CR>
    " æŸ¥æ‰¾å¹¶æ‰“å¼€æ–‡ä»¶ï¼Œç±»ä¼¼vimçš„findåŠŸèƒ½
    nmap csf :cs find f <C-R>=expand("<cfile>")<CR><CR>
    " æŸ¥æ‰¾åŒ…å«æœ¬æ–‡ä»¶çš„æ–‡ä»¶
    nmap csi :cs find i ^<C-R>=expand("<cfile>")<CR>$<CR>
    " è‡ªå·±æ¥è¾“å…¥å‘½ä»¤
    nmap cs<Space> :cs find 
  endif
endif
" æœ€å [[[1
if exists(':packadd')
  let &runtimepath = g:vimfiles . '/config,' . &runtimepath
endif
runtime abbreviate.vim
runtime local.vim
" vim:fdm=marker:fmr=[[[,]]]
